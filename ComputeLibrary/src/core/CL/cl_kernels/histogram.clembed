R"(

/*
 * Copyright (c) 2016, 2017 ARM Limited.
 *
 * SPDX-License-Identifier: MIT
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
/*
 * Copyright (c) 2016, 2017 ARM Limited.
 *
 * SPDX-License-Identifier: MIT
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to
 * deal in the Software without restriction, including without limitation the
 * rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
 * sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
#ifndef ARM_COMPUTE_HELPER_H
#define ARM_COMPUTE_HELPER_H

#pragma OPENCL EXTENSION cl_khr_fp16 : enable

#define CLAMP(x, min_val, max_val) min(max(x, min_val), max_val)

#define VEC_DATA_TYPE_STR(type, size) type##size
#define VEC_DATA_TYPE(type, size) VEC_DATA_TYPE_STR(type, size)

#define CONVERT_STR(x, type) (convert_##type((x)))
#define CONVERT(x, type) CONVERT_STR(x, type)

#define CONVERT_SAT_STR(x, type) (convert_##type##_sat((x)))
#define CONVERT_SAT(x, type) CONVERT_SAT_STR(x, type)

#define CONVERT_SAT_ROUND_STR(x, type, round) (convert_##type##_sat_##round((x)))
#define CONVERT_SAT_ROUND(x, type, round) CONVERT_SAT_ROUND_STR(x, type, round)

#define VECTOR_DECLARATION(name)     \
    __global uchar *name##_ptr,      \
    uint        name##_stride_x, \
    uint        name##_step_x,   \
    uint        name##_offset_first_element_in_bytes

#define IMAGE_DECLARATION(name)      \
    __global uchar *name##_ptr,      \
    uint        name##_stride_x, \
    uint        name##_step_x,   \
    uint        name##_stride_y, \
    uint        name##_step_y,   \
    uint        name##_offset_first_element_in_bytes

#define TENSOR3D_DECLARATION(name)   \
    __global uchar *name##_ptr,      \
    uint        name##_stride_x, \
    uint        name##_step_x,   \
    uint        name##_stride_y, \
    uint        name##_step_y,   \
    uint        name##_stride_z, \
    uint        name##_step_z,   \
    uint        name##_offset_first_element_in_bytes

#define CONVERT_TO_VECTOR_STRUCT(name) \
    update_vector_workitem_ptr(name##_ptr, name##_offset_first_element_in_bytes, name##_stride_x, name##_step_x)

#define CONVERT_TO_VECTOR_STRUCT_NO_STEP(name) \
    update_vector_workitem_ptr(name##_ptr, name##_offset_first_element_in_bytes, name##_stride_x, 0)

#define CONVERT_TO_IMAGE_STRUCT(name) \
    update_image_workitem_ptr(name##_ptr, name##_offset_first_element_in_bytes, name##_stride_x, name##_step_x, name##_stride_y, name##_step_y)

#define CONVERT_TO_IMAGE_STRUCT_NO_STEP(name) \
    update_image_workitem_ptr(name##_ptr, name##_offset_first_element_in_bytes, name##_stride_x, 0, name##_stride_y, 0)

#define CONVERT_TO_TENSOR3D_STRUCT(name)                                                                                                           \
    update_tensor3D_workitem_ptr(name##_ptr, name##_offset_first_element_in_bytes, name##_stride_x, name##_step_x, name##_stride_y, name##_step_y, \
                                 name##_stride_z, name##_step_z)

#define CONVERT_TO_TENSOR3D_STRUCT_NO_STEP(name) \
    update_tensor3D_workitem_ptr(name##_ptr, name##_offset_first_element_in_bytes, name##_stride_x, 0, name##_stride_y, 0, name##_stride_z, 0)

/** Structure to hold Vector information */
typedef struct Vector
{
    __global uchar *ptr;                           /**< Pointer to the starting postion of the buffer */
    int             offset_first_element_in_bytes; /**< The offset of the first element in the source image */
    int             stride_x;                      /**< Stride of the image in X dimension (in bytes) */
} Vector;

/** Structure to hold Image information */
typedef struct Image
{
    __global uchar *ptr;                           /**< Pointer to the starting postion of the buffer */
    int             offset_first_element_in_bytes; /**< The offset of the first element in the source image */
    int             stride_x;                      /**< Stride of the image in X dimension (in bytes) */
    int             stride_y;                      /**< Stride of the image in Y dimension (in bytes) */
} Image;

/** Structure to hold 3D tensor information */
typedef struct Tensor3D
{
    __global uchar *ptr;                           /**< Pointer to the starting postion of the buffer */
    int             offset_first_element_in_bytes; /**< The offset of the first element in the source image */
    int             stride_x;                      /**< Stride of the image in X dimension (in bytes) */
    int             stride_y;                      /**< Stride of the image in Y dimension (in bytes) */
    int             stride_z;                      /**< Stride of the image in Z dimension (in bytes) */
} Tensor3D;

/** Wrap vector information into an Vector structure, and make the pointer point at this workitem's data.
 *
 * @param[in] ptr                           Pointer to the starting postion of the buffer
 * @param[in] offset_first_element_in_bytes The offset of the first element in the source vector
 * @param[in] stride_x                      Stride of the vector in X dimension (in bytes)
 * @param[in] step_x                        stride_x * number of elements along X processed per workitem(in bytes)
 *
 * @return An image object
 */
Vector inline update_vector_workitem_ptr(__global uchar *ptr, uint offset_first_element_in_bytes, uint stride_x, uint step_x)
{
    Vector vector =
    {
        .ptr                           = ptr,
        .offset_first_element_in_bytes = offset_first_element_in_bytes,
        .stride_x                      = stride_x,
    };
    vector.ptr += vector.offset_first_element_in_bytes + get_global_id(0) * step_x;
    return vector;
}

/** Wrap image information into an Image structure, and make the pointer point at this workitem's data.
 *
 * @param[in] ptr                           Pointer to the starting postion of the buffer
 * @param[in] offset_first_element_in_bytes The offset of the first element in the source image
 * @param[in] stride_x                      Stride of the image in X dimension (in bytes)
 * @param[in] step_x                        stride_x * number of elements along X processed per workitem(in bytes)
 * @param[in] stride_y                      Stride of the image in Y dimension (in bytes)
 * @param[in] step_y                        stride_y * number of elements along Y processed per workitem(in bytes)
 *
 * @return An image object
 */
Image inline update_image_workitem_ptr(__global uchar *ptr, uint offset_first_element_in_bytes, uint stride_x, uint step_x, uint stride_y, uint step_y)
{
    Image img =
    {
        .ptr                           = ptr,
        .offset_first_element_in_bytes = offset_first_element_in_bytes,
        .stride_x                      = stride_x,
        .stride_y                      = stride_y
    };
    img.ptr += img.offset_first_element_in_bytes + get_global_id(0) * step_x + get_global_id(1) * step_y;
    return img;
}

/** Wrap 3D tensor information into an tensor structure, and make the pointer point at this workitem's data.
 *
 * @param[in] ptr                           Pointer to the starting postion of the buffer
 * @param[in] offset_first_element_in_bytes The offset of the first element in the source image
 * @param[in] stride_x                      Stride of the image in X dimension (in bytes)
 * @param[in] step_x                        stride_x * number of elements along X processed per workitem(in bytes)
 * @param[in] stride_y                      Stride of the image in Y dimension (in bytes)
 * @param[in] step_y                        stride_y * number of elements along Y processed per workitem(in bytes)
 * @param[in] stride_z                      Stride of the image in Z dimension (in bytes)
 * @param[in] step_z                        stride_z * number of elements along Z processed per workitem(in bytes)
 *
 * @return A 3D tensor object
 */
Tensor3D inline update_tensor3D_workitem_ptr(__global uchar *ptr, uint offset_first_element_in_bytes, uint stride_x, uint step_x, uint stride_y, uint step_y, uint stride_z, uint step_z)
{
    Tensor3D tensor =
    {
        .ptr                           = ptr,
        .offset_first_element_in_bytes = offset_first_element_in_bytes,
        .stride_x                      = stride_x,
        .stride_y                      = stride_y,
        .stride_z                      = stride_z
    };
    tensor.ptr += tensor.offset_first_element_in_bytes + get_global_id(0) * step_x + get_global_id(1) * step_y + get_global_id(2) * step_z;
    return tensor;
}

/** Get the pointer position of a Vector
 *
 * @param[in] vec Pointer to the starting position of the buffer
 * @param[in] x   Relative X position
 */
__global inline const uchar *vector_offset(const Vector *vec, int x)
{
    return vec->ptr + x * vec->stride_x;
}

/** Get the pointer position of a Image
 *
 * @param[in] img Pointer to the starting position of the buffer
 * @param[in] x   Relative X position
 * @param[in] y   Relative Y position
 */
__global inline uchar *offset(const Image *img, int x, int y)
{
    return img->ptr + x * img->stride_x + y * img->stride_y;
}

/** Get the pointer position of a Tensor3D
 *
 * @param[in] tensor Pointer to the starting postion of the buffer
 * @param[in] x      Relative X position
 * @param[in] y      Relative Y position
 * @param[in] z      Relative Z position
 */
__global inline const uchar *tensor3D_offset(const Tensor3D *tensor, int x, int y, int z)
{
    return tensor->ptr + x * tensor->stride_x + y * tensor->stride_y + z * tensor->stride_z;
}

#endif // _HELPER_H

#define VATOMIC_INC16(histogram, win_pos)   \
    {                                       \
        atomic_inc(histogram + win_pos.s0); \
        atomic_inc(histogram + win_pos.s1); \
        atomic_inc(histogram + win_pos.s2); \
        atomic_inc(histogram + win_pos.s3); \
        atomic_inc(histogram + win_pos.s4); \
        atomic_inc(histogram + win_pos.s5); \
        atomic_inc(histogram + win_pos.s6); \
        atomic_inc(histogram + win_pos.s7); \
        atomic_inc(histogram + win_pos.s8); \
        atomic_inc(histogram + win_pos.s9); \
        atomic_inc(histogram + win_pos.sa); \
        atomic_inc(histogram + win_pos.sb); \
        atomic_inc(histogram + win_pos.sc); \
        atomic_inc(histogram + win_pos.sd); \
        atomic_inc(histogram + win_pos.se); \
        atomic_inc(histogram + win_pos.sf); \
    }

/** Calculate the histogram of an 8 bit grayscale image.
 *
 * Each thread will process 16 pixels and use one local atomic operation per pixel.
 * When all work items in a work group are done the resulting local histograms are
 * added to the global histogram using global atomics.
 *
 * @note The input image is represented as a two-dimensional array of type uchar.
 * The output is represented as a one-dimensional uint array of length of num_bins
 *
 * @param[in]  input_ptr                           Pointer to the first source image. Supported data types: U8
 * @param[in]  input_stride_x                      Stride of the first source image in X dimension (in bytes)
 * @param[in]  input_step_x                        input_stride_x * number of elements along X processed per workitem(in bytes)
 * @param[in]  input_stride_y                      Stride of the first source image in Y dimension (in bytes)
 * @param[in]  input_step_y                        input_stride_y * number of elements along Y processed per workitem(in bytes)
 * @param[in]  input_offset_first_element_in_bytes The offset of the first element in the first source image
 * @param[in]  histogram_local                     The local buffer to hold histogram result in per workgroup. Supported data types: U32
 * @param[out] histogram                           The output buffer to hold histogram final result. Supported data types: U32
 * @param[out] num_bins                            The number of bins
 * @param[out] offset                              The start of values to use (inclusive)
 * @param[out] range                               The range of a bin
 * @param[out] offrange                            The maximum value (exclusive)
 */
__kernel void hist_local_kernel(IMAGE_DECLARATION(input),
                                __local uint *histogram_local,
                                __global uint *restrict histogram,
                                uint                    num_bins,
                                uint                    offset,
                                uint                    range,
                                uint                    offrange)
{
    Image input_buffer = CONVERT_TO_IMAGE_STRUCT(input);
    uint  local_id_x   = get_local_id(0);

    uint local_x_size = get_local_size(0);

    if(num_bins > local_x_size)
    {
        for(int i = local_id_x; i < num_bins; i += local_x_size)
        {
            histogram_local[i] = 0;
        }
    }
    else
    {
        if(local_id_x <= num_bins)
        {
            histogram_local[local_id_x] = 0;
        }
    }

    uint16 vals = convert_uint16(vload16(0, input_buffer.ptr));

    uint16 win_pos = select(num_bins, ((vals - offset) * num_bins) / range, (vals >= offset && vals < offrange));

    barrier(CLK_LOCAL_MEM_FENCE);
    VATOMIC_INC16(histogram_local, win_pos);
    barrier(CLK_LOCAL_MEM_FENCE);

    if(num_bins > local_x_size)
    {
        for(int i = local_id_x; i < num_bins; i += local_x_size)
        {
            atomic_add(histogram + i, histogram_local[i]);
        }
    }
    else
    {
        if(local_id_x <= num_bins)
        {
            atomic_add(histogram + local_id_x, histogram_local[local_id_x]);
        }
    }
}

/** Calculate the histogram of an 8 bit grayscale image's border.
 *
 * Each thread will process one pixel using global atomic.
 * When all work items in a work group are done the resulting local histograms are
 * added to the global histogram using global atomics.
 *
 * @note The input image is represented as a two-dimensional array of type uchar.
 * The output is represented as a one-dimensional uint array of length of num_bins
 *
 * @param[in]  input_ptr                           Pointer to the first source image. Supported data types: U8
 * @param[in]  input_stride_x                      Stride of the first source image in X dimension (in bytes)
 * @param[in]  input_step_x                        input_stride_x * number of elements along X processed per workitem(in bytes)
 * @param[in]  input_stride_y                      Stride of the first source image in Y dimension (in bytes)
 * @param[in]  input_step_y                        input_stride_y * number of elements along Y processed per workitem(in bytes)
 * @param[in]  input_offset_first_element_in_bytes The offset of the first element in the first source image
 * @param[out] histogram                           The output buffer to hold histogram final result. Supported data types: U32
 * @param[out] num_bins                            The number of bins
 * @param[out] offset                              The start of values to use (inclusive)
 * @param[out] range                               The range of a bin
 * @param[out] offrange                            The maximum value (exclusive)
 */
__kernel void hist_border_kernel(IMAGE_DECLARATION(input),
                                 __global uint *restrict histogram,
                                 uint                    num_bins,
                                 uint                    offset,
                                 uint                    range,
                                 uint                    offrange)
{
    Image input_buffer = CONVERT_TO_IMAGE_STRUCT(input);

    uint val = (uint)(*input_buffer.ptr);

    uint win_pos = (val >= offset) ? (((val - offset) * num_bins) / range) : 0;

    if(val >= offset && (val < offrange))
    {
        atomic_inc(histogram + win_pos);
    }
}

/** Calculate the histogram of an 8 bit grayscale image with bin size of 256 and window size of 1.
 *
 * Each thread will process 16 pixels and use one local atomic operation per pixel.
 * When all work items in a work group are done the resulting local histograms are
 * added to the global histogram using global atomics.
 *
 * @note The input image is represented as a two-dimensional array of type uchar.
 * The output is represented as a one-dimensional uint array of 256 elements
 *
 * @param[in]  input_ptr                           Pointer to the first source image. Supported data types: U8
 * @param[in]  input_stride_x                      Stride of the first source image in X dimension (in bytes)
 * @param[in]  input_step_x                        input_stride_x * number of elements along X processed per workitem(in bytes)
 * @param[in]  input_stride_y                      Stride of the first source image in Y dimension (in bytes)
 * @param[in]  input_step_y                        input_stride_y * number of elements along Y processed per workitem(in bytes)
 * @param[in]  input_offset_first_element_in_bytes The offset of the first element in the first source image
 * @param[in]  histogram_local                     The local buffer to hold histogram result in per workgroup. Supported data types: U32
 * @param[out] histogram                           The output buffer to hold histogram final result. Supported data types: U32
 */
__kernel void hist_local_kernel_fixed(IMAGE_DECLARATION(input),
                                      __local uint *histogram_local,
                                      __global uint *restrict histogram)
{
    Image input_buffer = CONVERT_TO_IMAGE_STRUCT(input);

    uint local_index  = get_local_id(0);
    uint local_x_size = get_local_size(0);

    for(int i = local_index; i < 256; i += local_x_size)
    {
        histogram_local[i] = 0;
    }

    uint16 vals = convert_uint16(vload16(0, input_buffer.ptr));

    barrier(CLK_LOCAL_MEM_FENCE);

    atomic_inc(histogram_local + vals.s0);
    atomic_inc(histogram_local + vals.s1);
    atomic_inc(histogram_local + vals.s2);
    atomic_inc(histogram_local + vals.s3);
    atomic_inc(histogram_local + vals.s4);
    atomic_inc(histogram_local + vals.s5);
    atomic_inc(histogram_local + vals.s6);
    atomic_inc(histogram_local + vals.s7);
    atomic_inc(histogram_local + vals.s8);
    atomic_inc(histogram_local + vals.s9);
    atomic_inc(histogram_local + vals.sa);
    atomic_inc(histogram_local + vals.sb);
    atomic_inc(histogram_local + vals.sc);
    atomic_inc(histogram_local + vals.sd);
    atomic_inc(histogram_local + vals.se);
    atomic_inc(histogram_local + vals.sf);

    barrier(CLK_LOCAL_MEM_FENCE);

    for(int i = local_index; i < 256; i += local_x_size)
    {
        atomic_add(histogram + i, histogram_local[i]);
    }
}

/** Calculate the histogram of an 8 bit grayscale image with bin size as 256 and window size as 1.
 *
 * Each thread will process one pixel using global atomic.
 * When all work items in a work group are done the resulting local histograms are
 * added to the global histogram using global atomics.
 *
 * @note The input image is represented as a two-dimensional array of type uchar.
 * The output is represented as a one-dimensional uint array of 256
 *
 * @param[in]  input_ptr                           Pointer to the first source image. Supported data types: U8
 * @param[in]  input_stride_x                      Stride of the first source image in X dimension (in bytes)
 * @param[in]  input_step_x                        input_stride_x * number of elements along X processed per workitem(in bytes)
 * @param[in]  input_stride_y                      Stride of the first source image in Y dimension (in bytes)
 * @param[in]  input_step_y                        input_stride_y * number of elements along Y processed per workitem(in bytes)
 * @param[in]  input_offset_first_element_in_bytes The offset of the first element in the first source image
 * @param[out] histogram                           The output buffer to hold histogram final result. Supported data types: U32
 */
__kernel void hist_border_kernel_fixed(IMAGE_DECLARATION(input),
                                       __global uint *restrict histogram)
{
    Image input_buffer = CONVERT_TO_IMAGE_STRUCT(input);
    atomic_inc(histogram + *input_buffer.ptr);
}

)"
